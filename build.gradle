plugins {
  id 'java'
  id 'org.jastadd' version '1.13.3'
}

defaultTasks 'jar'

if (!file('extendj/jastadd_modules').exists()) {
  throw new GradleException('ExtendJ seems to be missing. Please run "git submodule init", then "git submodule update".')
}

jastadd {
  configureModuleBuild()

  modules {
    include("extendj/jastadd_modules") // Include core ExtendJ modules.

    module "dit-extendj", {
      imports "java8 backend" // ExtendJ dependency for dit-extendj module.

      jastadd {
        basedir "src/jastadd/"
        include "**/*.ast"
        include "**/*.jadd"
        include "**/*.jrag"
      }

      scanner {
      	basedir "src/scanner"
	include "**/*.flex"
      }

      parser {
      	basedir "src/parser/"
	include "**/*.parser"
      }
    }
  }

  // Target module to build:
  module = 'dit-extendj'

  astPackage = 'org.extendj.ast'
  parser.name = 'JavaParser'
  scanner.name = 'OriginalScanner'
}

dependencies {
  testCompile 'junit:junit:4.12'
  testCompile 'com.google.truth:truth:0.30'
  testCompile 'org.apache.commons:commons-io:1.3.2'
}

sourceSets.main {
  java {
    srcDir 'extendj/src/frontend-main'
    srcDir 'extendj/src/backend-main'
  }
  resources {
    srcDir 'extendj/src/res'
    srcDir jastadd.buildInfoDir
  }
}

test {
  description 'Run all tests except runtime tests.'
  outputs.upToDateWhen { false } // Always re-run tests.
  testLogging.exceptionFormat = "full"
}

// Use JavaCompiler if you need bytecode generation.
//jar.manifest.attributes 'Main-Class': 'org.extendj.JavaChecker'
jar.manifest.attributes 'Main-Class': 'org.extendj.JavaCompiler'
jar.destinationDir = projectDir

// Java -source and -target version.
sourceCompatibility = targetCompatibility = '1.8'

task sourceZip(type: Zip) {
  description 'Builds a Zip file with the entire repisotory (including the ExtendJ submodule).'
  destinationDir = projectDir
  archiveName = "dit-extendj-src.zip"

  from (projectDir) {
    exclude '**/.git'
    exclude '**/.gitignore'
    exclude '**/.gitattributes'
    exclude '**/.gitmodules'
    exclude 'build'
    exclude 'bin'
    exclude '.gradle'
    exclude '.classpath'
    exclude '.settings'
    exclude '.project'
    exclude '*.jar'
    exclude '*.zip'
    exclude '**/*.swp'
  }

  into 'dit-extendj'
}
